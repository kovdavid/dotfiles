#!/usr/bin/env perl

use utf8;
use v5.16;
use strict;
use warnings;

use JSON::XS;

$| = 1;

# Skip the first line which contains the version header.
print scalar <STDIN>;

# The second line contains the start of the infinite array.
print scalar <STDIN>;

my $layer_colors = [
    "#FFFFFF",
    "#00FF00",
    "#FFFF00",
    "#FF0000",
    "#FF00FF",
];

my $dalsik_layer_change = "/tmp/dalsik_layer_change";
my $dalsik_modifier_change = "/tmp/dalsik_modifier_change";

# Read lines forever, ignore a comma at the beginning if it exists.
while (my ($statusline) = (<STDIN> =~ /^,?(.*)/)) {
    my @blocks = @{decode_json($statusline)};

    if ( -f $dalsik_modifier_change ) {
        open my $fh, "<", $dalsik_modifier_change;
        my @lines = <$fh>;
        close $fh;

        for my $line (reverse @lines) {
            if ($line =~ m/^(\w+) (\d)/) {
                my $mod = $1;
                my $pressed = $2;
                unshift @blocks, {
                    full_text => $mod,
                    color => $pressed ? "#FF0000" : "#FFFFFF",
                };
            }
        }
    }
    if ( -f $dalsik_layer_change ) {
        open my $fh, "<", $dalsik_layer_change;
        my $layer = <$fh>;
        close $fh;

        unshift @blocks, {
                full_text => "L[$layer]",
                color => $layer_colors->[$layer] || $layer_colors->[0],
        };
    }

    print encode_json(\@blocks) . ",\n";
}
